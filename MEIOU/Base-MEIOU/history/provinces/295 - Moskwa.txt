owner = MOS
controller = MOS
add_core = MOS
capital = "Moskva"
trade_goods = wool
culture = russian
religion = orthodox
hre = no
base_tax = 12
base_production = 3
base_manpower = 1
is_city = yes
fort_14th = yes
marketplace = yes
urban_infrastructure_1 = yes
workshop = yes
road_network = yes
temple = yes
discovered_by = eastern
discovered_by = western
discovered_by = steppestech
discovered_by = turkishtech
discovered_by = muslim
450.1.1 = {
	set_variable = {
		which = pop_level
		value = 4
	}
	set_province_flag = has_confluence
	set_province_flag = good_natural_place
	add_permanent_province_modifier = {
		name = "moskva_geography"
		duration = -1
	}
}
500.1.1 = {
	add_permanent_province_modifier = {
		name = urban_goods_metalwork
		duration = -1
	}
}
1434.5.6 = {
	revolt = {
		type = pretender_rebels
		size = 2
		name = "Vasily Yurievich"
	}
	controller = REB
}
1436.1.1 = {
	revolt = {}
	controller = MOS
}
1505.1.1 = {
	fort_14th = yes
}
1515.1.1 = {
	base_production = 6
}
1547.1.1 = {
	owner = RUS
	controller = RUS
	add_core = RUS
	remove_core = MOS
}
1598.1.1 = {
	unrest = 5
}
1610.9.27 = {
	controller = POL
}
1612.11.4 = {
	controller = RUS
}
1613.1.1 = {
	unrest = 0
}
1670.1.1 = {
	unrest = 8
}
1671.1.1 = {
	unrest = 0
}
1773.1.1 = {
	unrest = 5
}
1774.9.14 = {
	unrest = 0
}
1812.6.28 = {
	controller = FRA
}
1812.12.10 = {
	controller = RUS
}
